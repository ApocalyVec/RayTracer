<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8" />
    <title>Ray Tracer</title>

    <style>
        canvas {
            padding-left: 0;
            padding-right: 0;
            margin-left: auto;
            margin-right: auto;
            display: block;
        }
        .slider {
            -webkit-appearance: none;
            width: 100%;
            height: 25px;
            background: #d3d3d3;
            outline: none;
            opacity: 0.7;
            -webkit-transition: .2s;
            transition: opacity .2s;
        }
        .slider:hover {
            opacity: 1;
        }
        .slider::-webkit-slider-thumb {
            -webkit-appearance: none;
            appearance: none;
            width: 25px;
            height: 25px;
            background: #4CAF50;
            cursor: pointer;
        }
        .slider::-moz-range-thumb {
            width: 25px;
            height: 25px;
            background: #4CAF50;
            cursor: pointer;
        }
    </style>

<script id="vshader" type="x-shader/x-vertex">
   attribute vec3 aPosition;
   attribute vec2 a_texcoord;

   varying   vec3 vPosition;

   // user inputs
   uniform float sceneNum;
   varying float fsceneNum;

   uniform float vSphereShininess;
   varying float sphereShininess;

   uniform float vPlaneShininess;
   varying float planeShininess;

   uniform float vNumBounces;
   varying float bounceNum;

//   uniform mat4 u_matrix;
   varying vec2 v_texcoord;

   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;

       fsceneNum = sceneNum;
       sphereShininess = vSphereShininess;
       planeShininess = vPlaneShininess;
       bounceNum = vNumBounces;

       v_texcoord = a_texcoord;
   }
</script>

<script id="fshader" type="x-shader/x-fragment">
    precision highp float;
    uniform float uTime;
    varying float fsceneNum;
    varying float planeShininess;
    varying float sphereShininess;
    varying float bounceNum;
    uniform float f_isSoftShadow;
    uniform float f_lightIntensity;
    uniform float f_lightRadius;

    uniform float f_lightX;
    uniform float f_lightY;
    uniform float f_lightZ;

    varying vec3 vPosition;
    uniform sampler2D u_texture;
    varying vec2 v_texcoord;

    // Noise
    vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
    vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
    vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
    vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
    vec3 fade(vec3 t) { return t*t*t*(t*(t*6.0-15.0)+10.0); }
    float noise(vec3 P) {
        vec3 i0 = mod289(floor(P)), i1 = mod289(i0 + vec3(1.0));
        vec3 f0 = fract(P), f1 = f0 - vec3(1.0), f = fade(f0);
        vec4 ix = vec4(i0.x, i1.x, i0.x, i1.x), iy = vec4(i0.yy, i1.yy);
        vec4 iz0 = i0.zzzz, iz1 = i1.zzzz;
        vec4 ixy = permute(permute(ix) + iy), ixy0 = permute(ixy + iz0), ixy1 = permute(ixy + iz1);
        vec4 gx0 = ixy0 * (1.0 / 7.0), gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
        vec4 gx1 = ixy1 * (1.0 / 7.0), gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
        gx0 = fract(gx0); gx1 = fract(gx1);
        vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0), sz0 = step(gz0, vec4(0.0));
        vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1), sz1 = step(gz1, vec4(0.0));
        gx0 -= sz0 * (step(0.0, gx0) - 0.5); gy0 -= sz0 * (step(0.0, gy0) - 0.5);
        gx1 -= sz1 * (step(0.0, gx1) - 0.5); gy1 -= sz1 * (step(0.0, gy1) - 0.5);
        vec3 g0 = vec3(gx0.x,gy0.x,gz0.x), g1 = vec3(gx0.y,gy0.y,gz0.y),
        g2 = vec3(gx0.z,gy0.z,gz0.z), g3 = vec3(gx0.w,gy0.w,gz0.w),
        g4 = vec3(gx1.x,gy1.x,gz1.x), g5 = vec3(gx1.y,gy1.y,gz1.y),
        g6 = vec3(gx1.z,gy1.z,gz1.z), g7 = vec3(gx1.w,gy1.w,gz1.w);
        vec4 norm0 = taylorInvSqrt(vec4(dot(g0,g0), dot(g2,g2), dot(g1,g1), dot(g3,g3)));
        vec4 norm1 = taylorInvSqrt(vec4(dot(g4,g4), dot(g6,g6), dot(g5,g5), dot(g7,g7)));
        g0 *= norm0.x; g2 *= norm0.y; g1 *= norm0.z; g3 *= norm0.w;
        g4 *= norm1.x; g6 *= norm1.y; g5 *= norm1.z; g7 *= norm1.w;
        vec4 nz = mix(vec4(dot(g0, vec3(f0.x, f0.y, f0.z)), dot(g1, vec3(f1.x, f0.y, f0.z)),
        dot(g2, vec3(f0.x, f1.y, f0.z)), dot(g3, vec3(f1.x, f1.y, f0.z))),
        vec4(dot(g4, vec3(f0.x, f0.y, f1.z)), dot(g5, vec3(f1.x, f0.y, f1.z)),
        dot(g6, vec3(f0.x, f1.y, f1.z)), dot(g7, vec3(f1.x, f1.y, f1.z))), f.z);
        return 2.2 * mix(mix(nz.x,nz.z,f.y), mix(nz.y,nz.w,f.y), f.x);
    }
    float noise(vec2 P) { return noise(vec3(P, 0.0)); }

    struct Sphere {
        vec3 center;
        float radius;
        vec3 color;
    };

    struct Triangle {
        vec3 v1;
        vec3 v2;
        vec3 v3;
        vec3 normal;
        vec3 color;
    };

    struct Plane {
        vec3 normal;
        vec3 p;
        vec3 color;
        vec3 lightingNormal;
    };

    struct Ray {
        vec3 origin;
        vec3 direction;
    };

    struct Light {
        vec3 position;
        float radius;

        float ambience;
        vec3 specular;
        vec3 diffuse;
    };

    const int objNum = 10;

    Ray rays[1];
    Light light[objNum];

    Sphere spheres[objNum];
    Triangle triangles[objNum];
    Plane planes[objNum];

    float sphereAbsorbList[objNum];

    const float triangleAbsorb = 0.5;
    const float planeAbsorb = 1.0 - 1e-32;

    vec3 newell(vec3 v1, vec3 v2, vec3 v3) {
        float n1, n2, n3;

        n1 = (v1[1] - v2[1]) * (v1[2] + v2[2]) +
        (v2[1] - v3[1]) * (v2[2] + v3[2]) +
        (v3[1] - v1[1]) * (v3[2] + v1[2]);

        n2 = (v1[2] - v2[2]) * (v1[0] + v2[0]) +
        (v2[2] - v3[2]) * (v2[0] + v3[0]) +
        (v3[2] - v1[2]) * (v3[0] + v1[0]);

        n3 = (v1[0] - v2[0]) * (v1[1] + v2[1]) +
        (v2[0] - v3[0]) * (v2[1] + v3[1]) +
        (v3[0] - v1[0]) * (v3[1] + v1[1]);

        vec3 normal = normalize(vec3(n1, n2, n3));

        return normal;
    }

    float x; // pixel positions x
    float y; // pixel positions y
    float z; // pixel positions z

    float focalLength;

    void initialize() {
        x = vPosition.x; // get pixel positions x
        y = vPosition.y; // get pixel positions y
        z = vPosition.z; // get pixel positions z
        vec3 color = vec3(0.0, 0.0, 0.0);

        light[0].radius = f_lightRadius;
        light[0].position = vec3(f_lightX, f_lightZ, f_lightY);

        if (fsceneNum == 1.0) {
            focalLength = 1.5;
            // aobsorb factor for the spheres when reflecting
            for (int i = 0; i < objNum; i ++) {
                sphereAbsorbList[i] = 0.5;
            }

            // Create spheres
            spheres[0].center = vec3(-.1, .2, 0);
            spheres[0].radius = 0.2;
            spheres[0].color = vec3(1.0, 0.0, 0.0);

            // create a second sphere
            spheres[1].center = vec3(.3, .2, -.4);
            spheres[1].radius = 0.2;
            spheres[1].color = vec3(0.0, 1.0, 0.0);

            // create Triangles
            triangles[0].v1 = vec3(2.0, -.6, 2.0);
            triangles[0].v2 = vec3(-2.0, -.6, 2.0);
            triangles[0].v3 = vec3(-2.0, -.6, -2.0);
            triangles[0].normal = -newell(triangles[0].v1, triangles[0].v2, triangles[0].v3);
            triangles[0].color = vec3(0.0, 0.0, 1.0);

            triangles[1].v1 = vec3(2.0, -.6, 2.0);
            triangles[1].v2 = vec3(-2.0, -.6, -2.0);
            triangles[1].v3 = vec3(2.0, -.6, -2.0);
            triangles[1].normal = -newell(triangles[1].v1, triangles[1].v2, triangles[1].v3);
            triangles[1].color = vec3(0.0, 0.0, 1.0);

            // Create ray
            rays[0].origin = vec3(.2, .3, focalLength);
            rays[0].direction = normalize(vec3(x, y, -focalLength));

            // Create Light source
            light[0].ambience = 0.3;
        }
        else if (fsceneNum == 2.0) {
            // aobsorb factor for the spheres when reflecting
            for (int i = 0; i < objNum; i ++) {
                sphereAbsorbList[i] = 0.6;
            }

            focalLength = 2.0;

            float spherePos = 0.3;
            float sphereRad = 0.25;

            // Create spheres
            spheres[0].center = vec3(-spherePos, -spherePos, 0.5);
            spheres[0].radius = sphereRad;
            spheres[0].color = vec3(1.0, 0.0, 0.0);

            // create a second sphere
            spheres[1].center = vec3(spherePos, spherePos, 0.5);
            spheres[1].radius = sphereRad;
            spheres[1].color = vec3(0.0, 1.0, 0.0);

            // create a third sphere
            spheres[2].center = vec3(-spherePos, spherePos, 0.5);
            spheres[2].radius = sphereRad;
            spheres[2].color = vec3(1.0, 1.0, 0.0);

            // the forth sphere
            spheres[3].center = vec3(spherePos, -spherePos, .5);
            spheres[3].radius = sphereRad;
            spheres[3].color = vec3(0.0, 0.0, 1.0);

            // Create ray
            rays[0].origin = vec3(.0, .0, focalLength);
            rays[0].direction = normalize(vec3(x, y, -focalLength));

            // Create Light source
            light[0].ambience = 0.2;

        }
        else if (fsceneNum == 3.0) {
            // aobsorb factor for the spheres when reflecting
            for (int i = 0; i < objNum; i ++) {
                sphereAbsorbList[i] = 1.0 - 1e-32;
            }
            // some spheres are completely reflective
            sphereAbsorbList[1] = 0.001;
            sphereAbsorbList[6] = 0.001;
            sphereAbsorbList[7] = 0.001;

            focalLength = 2.0;

            // Create spheres
            spheres[0].center = vec3(-.4, -.4, 0.0);
            spheres[0].radius = 0.1;
            spheres[0].color = vec3(0.0, 0.0, 1.0);

            spheres[1].center = vec3(.3, -.3, -0.2);
            spheres[1].radius = 0.2;
            spheres[1].color = vec3(0.0, 0.0, 0.0);

            spheres[2].center = vec3(-.3, -.43, 0.15);
            spheres[2].radius = 0.07;
            spheres[2].color = vec3(0.0, 0.4, 1.0);

            spheres[3].center = vec3(-.2, -.45, 0.2);
            spheres[3].radius = 0.05;
            spheres[3].color = vec3(0.0, 0.8, 1.0);

            spheres[4].center = vec3(-.4, -.2, 0.0);
            spheres[4].radius = 0.1;
            spheres[4].color = vec3(0.3, 0.0, 1.0);

            spheres[5].center = vec3(-.4, -.0, 0.0);
            spheres[5].radius = 0.1;
            spheres[5].color = vec3(0.6, 0.1, 1.0);

            spheres[6].center = vec3(-.4, .2, 0.0);
            spheres[6].radius = -.1;
            spheres[6].color = vec3(0.9, 0.0, 1.0);

            spheres[7].center = vec3(0.2, -.47, 0.7);
            spheres[7].radius = -.03;
            spheres[7].color = vec3(0.0, 1.0, 1.0);


            // create plane
            // bottom plane
            planes[0].normal = normalize(vec3(0.0, 1.0, 0.0));
            planes[0].p = vec3(0.0, -0.5, 0.0);
            planes[0].color = vec3(0.0, 0.0, 1.0);
            planes[0].lightingNormal = vec3(0.0, 1.0, 1.0);

            //top plane
            planes[1].normal = normalize(vec3(0.0, -1.0, 0.0));
            planes[1].p = vec3(0.0, 0.4, 0.0);
            planes[1].color = vec3(1.0, 1.0, 0.0);
            planes[1].lightingNormal = vec3(0.0, 1.0, 1.0);

            // back plane
            planes[2].normal = normalize(vec3(0.0, 0.0, 1.0));
            planes[2].p = vec3(0.0, 0.0, -0.5);
            planes[2].color = vec3(0.0, 1.0, 1.0);
            planes[2].lightingNormal = vec3(0.0, 1.0, 1.0);

            //front plane
            planes[3].normal = normalize(vec3(0.0, 0.0, -1.0));
            planes[3].p = vec3(0.0, 0.0, 0.5);
            planes[3].color = vec3(1.0, 1.0, 1.0);
            planes[3].lightingNormal = vec3(0.0, 1.0, 1.0);

            //right plane
            planes[4].normal = normalize(vec3(-1.0, 0.0, 0.1));
            planes[4].p = vec3(.5, 0.0, 0.0);
            planes[4].color = vec3(1.0, 0.0, 0.0);
            planes[4].lightingNormal = vec3(0.0, 1.0, 1.0);

            //left plane
            planes[5].normal = normalize(vec3(1.0, 0.0, 0.1));
            planes[5].p = vec3(-.5, 0.0, 0.0);
            planes[5].color = vec3(1.0, 0.0, 1.0);
            planes[5].lightingNormal = vec3(0.0, 1.0, 1.0);

            // Create ray
            rays[0].origin = vec3(.0, .0, focalLength);
            rays[0].direction = normalize(vec3(x, y, -focalLength));

            // Create Light source
            light[0].ambience = 0.2;
        }
    }

    const float veryBigNum = 1e23;
    const float verySmallNum = 1e-23;

    const float lightShininess = verySmallNum;

    vec3 lightColor = vec3(1.0);

    bool checkIntersectSphere(Sphere sphere, Ray ray, out float t) {
        t = veryBigNum;

        vec3 sphereCenter = sphere.center;
        float radius = sphere.radius;
        vec3 cameraSource = ray.origin;
        vec3 cameraDirection = ray.direction;

        vec3 distanceFromCenter = (cameraSource - sphereCenter);
        float B = 2.0 * dot(cameraDirection, distanceFromCenter);
        float C = dot(distanceFromCenter, distanceFromCenter) - pow(radius, 2.0);
        float delta = pow(B, 2.0) - 4.0 * C;
        if (delta > 0.0) {
            float sqRoot = sqrt(delta);
            float t1 = (-B + sqRoot) / 2.0;
            float t2 = (-B - sqRoot) / 2.0;
            t = min(t1, t2);
        }
        if (delta == 0.0) {
            t = -B / 2.0;
        }

        if (t > 0.0 && t < veryBigNum) {
            return true;
        }
        else {
            return false;
        }
    }

    bool checkIntersectLight(Light light, Ray ray, out float t) {
        t = veryBigNum;

        vec3 sphereCenter = light.position;
        float radius = light.radius;
        vec3 cameraSource = ray.origin;
        vec3 cameraDirection = ray.direction;

        vec3 distanceFromCenter = (cameraSource - sphereCenter);
        float B = 2.0 * dot(cameraDirection, distanceFromCenter);
        float C = dot(distanceFromCenter, distanceFromCenter) - pow(radius, 2.0);
        float delta = pow(B, 2.0) - 4.0 * C;
        if (delta > 0.0) {
            float sqRoot = sqrt(delta);
            float t1 = (-B + sqRoot) / 2.0;
            float t2 = (-B - sqRoot) / 2.0;
            t = min(t1, t2);
        }
        if (delta == 0.0) {
            t = -B / 2.0;
        }

        if (t > 0.0 && t < veryBigNum) {
            return true;
        }
        else {
            return false;
        }
    }


    bool checkIntersectPlane(Plane plane, Ray ray, out float t) {

        vec3 n = -plane.normal;
        vec3 l = ray.direction;
        vec3 l0 = ray.origin;
        vec3 p0 = plane.p;

        float denom = dot(n, l);
        if (denom > 1e-6) {
            vec3 p0l0 = p0 - l0;
            t = dot(p0l0, n) / denom;
            return (t >= 0.0);
        }

        return false;
    }

    // adopted from
    // https://www.scratchapixel.com/lessons/3d-basic-rendering/ray-tracing-rendering-a-triangle/ray-triangle-intersection-geometric-solution
    bool checkIntersectTriangle(Triangle triangle, Ray ray, out float t) {

        vec3 v0 = triangle.v1;
        vec3 v1 = triangle.v2;
        vec3 v2 = triangle.v3;

        vec3 orig = ray.origin;
        vec3 dir = ray.direction;

        // compute the pane's normal
        vec3 v0v1 = v1 - v0;
        vec3 v0v2 = v2 - v0;
        // no need to normalize
        vec3 N = cross(v0v1, v0v2);

        // check if the ray and the plane are parallel
        float NdotRayDirection = dot(N, dir);
        if(abs(NdotRayDirection) < verySmallNum) return false;  // the ray and the plane does not intersect because they are parallel

        // compute d
        float d = dot(N, v0); // distance from the origin to the plane
        //compute t
        t = (dot(N, orig) + d) / NdotRayDirection;

        //check if the triangle is behind the ray
        if (t < 0.0) return false;  // the triangle is behind

        //compute the intersection point
        vec3 P = orig + t * dir;

        vec3 C; // vector perpendicular to triangle's plane

        // edge 0
        vec3 edge0 = v1 - v0;
        vec3 vp0 = P - v0;
        C = cross(edge0, vp0);
        if (dot(C, N) < 0.0) return false; // P is on the right side

        // edge 1
        vec3 edge1 = v2 - v1;
        vec3 vp1 = P - v1;
        C = cross(edge1, vp1);
        if (dot(C, N) < 0.0)  return false; // P is on the right side

        // edge 2
        vec3 edge2 = v0 - v2;
        vec3 vp2 = P - v2;
        C = cross(edge2, vp2);
        if (dot(C, N) < 0.0) return false; // P is on the right side;

        return true;
    }

    bool checkIntersect(Ray ray, out float minT, out vec3 surfacePoint, out vec3 surfaceNormal, out vec3 lightingNormal,
    out vec3 cameraSource, out vec3 cameraDirection, out vec3 objColor, out float shininess, out float absorb, out bool isLightSource) {
        float t;
        float sphereMinT = veryBigNum;
        float triangleMinT = veryBigNum;
        float planeMinT = veryBigNum;
        float lightMinT = veryBigNum;

        float sphereAbsorb;

        minT =  veryBigNum;

        Sphere sphereToShow;
        Triangle triangleToShow;
        Plane planeToShow;
        Light lightToShow;

        cameraSource = ray.origin;
        cameraDirection = ray.direction;

        // check sphere intersection
        for(int i = 0; i < objNum; i++) {
            if (checkIntersectSphere(spheres[i], ray, t)) {
                if (t < sphereMinT) {
                    sphereMinT = t;
                    sphereToShow = spheres[i];
                    sphereAbsorb = sphereAbsorbList[i];
                }
            }
        }
        //check triangle intersection
        for(int i = 0; i < objNum; i++) {
            if (checkIntersectTriangle(triangles[i], ray, t)) {
                if(t < triangleMinT) {
                    triangleMinT = t;
                    triangleToShow = triangles[i];
                }
            }
        }

        // check plane intersection
        for(int i = 0; i < objNum; i ++) {
            if (checkIntersectPlane(planes[i], ray, t)) {
                if (t < planeMinT) {
                    planeMinT = t;
                    planeToShow = planes[i];
                }
            }
        }

        // check light source intersection
        for(int i = 0; i < objNum; i++) {
            if (checkIntersectLight(light[i], ray, t)) {
                if (t < sphereMinT) {
                    lightMinT = t;
                    lightToShow = light[i];
                }
            }
        }

        isLightSource = false;

        if(min(lightMinT, min(planeMinT, min(sphereMinT, triangleMinT))) == sphereMinT && sphereMinT < veryBigNum) { // if the closest interect is a sphere
            minT = sphereMinT;
            // sphere absorb is already processed in 'check sphere intersection'
            absorb = sphereAbsorb;

            vec3 sphereCenter = sphereToShow.center;
            objColor = sphereToShow.color;
            surfacePoint = cameraSource + (minT * cameraDirection);
            surfaceNormal = lightingNormal = normalize(surfacePoint - sphereCenter);
            shininess = sphereShininess;
            return true;
        }
        else if (min(lightMinT, min(planeMinT, min(sphereMinT, triangleMinT))) == triangleMinT && triangleMinT < veryBigNum){  // if the closest interect is a triangle
            minT = triangleMinT;
            absorb = triangleAbsorb;

            objColor = triangleToShow.color;
            surfacePoint = cameraSource + (minT * cameraDirection);
            surfaceNormal = lightingNormal = normalize(triangleToShow.normal);
            shininess = planeShininess;
            return true;
        }

        else if (min(lightMinT, min(planeMinT, min(sphereMinT, triangleMinT))) == planeMinT && planeMinT < veryBigNum){  // if the closest interect is a plane
            minT = planeMinT;
            absorb = planeAbsorb;

            objColor = planeToShow.color;
            surfacePoint = cameraSource + (minT * cameraDirection);
            surfaceNormal = planeToShow.normal;
            lightingNormal = planeToShow.lightingNormal;
            shininess = planeShininess;
            return true;
        }
        else if (min(lightMinT, min(planeMinT, min(sphereMinT, triangleMinT))) == lightMinT && lightMinT < veryBigNum){  // if the closest interect is a plane
            minT = lightMinT;
            isLightSource = true;
            objColor = vec3(1.0, 1.0, 1.0);  // light is white

            return true;
        }

            return false;
    }

    bool checkIntersectShadow(Ray ray, out float shadow_det) {

        float t = 0.0;
        float minT = veryBigNum;
        float sphereMinT = veryBigNum;
        float triangleMinT = veryBigNum;
        float planeMinT = veryBigNum;
        float lightMinT = veryBigNum;

        bool isIntersect = false;



        // check sphere intersection
//        for(int i = 0; i < objNum; i++) {
//            isIntersect = isIntersect || (checkIntersectSphere(spheres[i], ray, t));
//        }
//        //check triangle intersection
//        for(int i = 0; i < objNum; i++) {
//            isIntersect = isIntersect || ( checkIntersectTriangle(triangles[i], ray, t));
//        }

        // check sphere intersection
        for(int i = 0; i < objNum; i++) {
            if (checkIntersectSphere(spheres[i], ray, t)) {
                if (t < sphereMinT) {
                    sphereMinT = t;
                }
            }
        }
        //check triangle intersection
        for(int i = 0; i < objNum; i++) {
            if (checkIntersectTriangle(triangles[i], ray, t)) {
                if(t < triangleMinT) {
                    triangleMinT = t;
                }
            }
        }

        // check plane intersection
        for(int i = 0; i < objNum; i ++) {
            if (checkIntersectPlane(planes[i], ray, t)) {
                if (t < planeMinT) {
                    planeMinT = t;
                }
            }
        }

        // check light source intersection
        for(int i = 0; i < objNum; i++) {
            if (checkIntersectLight(light[i], ray, t)) {
                if (t < sphereMinT) {
                    lightMinT = t;
                }
            }
        }

        return !(min(lightMinT, min(planeMinT, min(sphereMinT, triangleMinT))) == lightMinT);
    }

    // adopted from https://stackoverflow.com/questions/4200224/random-noise-functions-for-glsl
    float simpleRand(vec2 co){
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }

    vec3 normRandVec3() {
        return normalize(vec3(noise(vec3(x * 100.0, y * 100.0, simpleRand(vec2(x, y))))));
    }

    vec2 rand2Uniform() {

        return normRandVec3().xy;
    }

    vec3 getRay(Ray ray, Light light) {

         vec3 lightSource = light.position;
         float ambience = light.ambience;

         vec3 objColor;
         float minT;
         vec3 surfacePoint;
         vec3 surfaceNormal;
        vec3 lightingNormal;
         vec3 cameraSource;
         vec3 cameraDirection;
         float shininess;
         float absorb;

        bool isLightSource;

         float veryBigNum = 1e38;

         Ray reflectionRay;
         vec3 accumColor = vec3(0.0);

        const int numBounces = 100;

        float shadow_det;

        vec3 mask = vec3(1.0);

         for (int i = 0; i < numBounces; i ++) {
             if (i >= int(bounceNum)) break;  // if the number of reflection is reached, terminate the loop


             if(checkIntersect(ray, minT, surfacePoint, surfaceNormal, lightingNormal, cameraSource, cameraDirection, objColor, shininess, absorb, isLightSource)) {
                 if(isLightSource) { // if we hit the light source
                    return lightColor;
                 }

                 // deal with reflection
                 vec3 reflectionDir =reflect(ray.direction, surfaceNormal); // this is the reflection vector
                 reflectionRay.origin = surfacePoint + 0.00001 * reflectionDir;  // avoid z fighting
                 //                  try refraction
                 //                 vec3 reflectionDir =refract(ray.direction, surfaceNormal, 0.98); // this is the reflection vector
                 //                 reflectionRay.origin = surfacePoint - 0.00001 * reflectionDir;  // avoid z fighting

                 reflectionRay.direction = reflectionDir;

                 if(f_isSoftShadow == 1.0) {

                     vec3 lightDirection = normalize(lightSource  - surfacePoint);

                     float _minT;
                     vec3 _objColor;
                     vec3 _surfacePoint;
                     vec3 _surfaceNormal;  // surface normal on the light source
                     vec3 _lightingNormal;
                     vec3 _cameraSource;
                     vec3 _cameraDirection;
                     float _shininess;
                     float _absorb;
                     bool _isLightSource;

                     vec3 lp = light.position + normRandVec3() * light.radius; // lp: random position on the surface of the light source
                     vec3 ldir = normalize(lp - reflectionRay.origin);
                     Ray lray;
                     lray.origin = surfacePoint + ldir * 0.0001;
                     lray.direction = ldir;

                     bool isIntersect = checkIntersect(lray, _minT, _surfacePoint, _surfaceNormal, _lightingNormal, _cameraSource, _cameraDirection, _objColor, _shininess, _absorb, _isLightSource);
                     if(isIntersect && _isLightSource) { // if the ray hit a light source

                         float d = clamp(dot(lightingNormal, ldir), 0.0, 1.0); // calculate the impact of the anlge of incidence between the light source surface norm and the light ray direction
                         d *= pow(asin(light.radius/distance(surfacePoint, light.position)), 2.0);

                         float specular = pow(max(dot(reflectionDir, lightDirection), 0.0), shininess);
                         float diffuse = max(0.0, dot(lightingNormal, lightSource));

                         // ternary: light is absorb by the object after reflection
                         accumColor += f_lightIntensity * d * (specular + objColor) * mask;//*(i > 1 ? absorb : 1.0);
                     }

                 }
                 else {
                     vec3 lightDirection = normalize(lightSource  - surfacePoint);
                     Ray toLightRay;
                     toLightRay.origin = reflectionRay.origin;
                     toLightRay.direction = lightDirection;
                     // check if the point is in shadow
                     if(checkIntersectShadow(toLightRay, shadow_det)) {
                         accumColor = accumColor + objColor * ambience;
                     }
                     else {
                         float specular = pow(max(dot(reflectionDir, lightDirection), 0.0), shininess);
                         float diffuse = max(0.0, dot(lightingNormal, lightSource));
                         // ternary: light is absorb by the object after reflection
                         accumColor = accumColor + objColor * (ambience + (1.0 - ambience) * diffuse)*(i > 1 ? absorb : 1.0);
                         accumColor = accumColor + lightColor * specular * (absorb)*(i > 1 ? absorb : 1.0);
                     }
                 }

             }

              else {  // the ray did not hit anything
                  break;
              }

             if((1.0 - absorb) < verySmallNum) {
                 break;
             }
             // continue with the reflected ray
             ray = reflectionRay;
         }

         return accumColor;
     }

    void main() {
        initialize();
        vec3 color = getRay(rays[0], light[0]);

        if (f_isSoftShadow == 1.0) {
            float resolution = 800.0;

            // texture map of the last frame
            vec3 src = texture2D(u_texture, v_texcoord).rgb;

            vec2 jitter = rand2Uniform() - 0.5;
            vec2 px = 2.0 * (gl_FragCoord.xy + jitter)/resolution - 1.0;
            rays[0].direction =  normalize(vec3(px, -focalLength));

            gl_FragColor = vec4(color + src - vec3(0.0, 0.0, 1.0), 1.0);
        }
        else {
            gl_FragColor = vec4(color, 1.0);

        }
   }
</script>

    <script src="lib/webgl-utils.js"></script>
    <script src="lib/initShaders.js"></script>
    <script src="lib/MV.js"></script>

    <script src="main.js"></script>

</head>

<body onload="main()">

<canvas id="webgl" width="800" height="800" >
    Please use a browser that supports the "canvas" tag.
</canvas>
<div style="text-align: center; margin: 30px">
    <h2>
        <span style="font-weight:bold">Use number key 1~3 to switch between scenes.</span>
    </h2>
    <h2>
        <span style="font-weight:bold">Press s to toggle softshadow.</span>
    </h2>
</div>

<div id = "sliderContainer">

    <input type="range" min="1.0" max="100.0" value="20.0" class="slider" id="sphereShininessSlider" name="sphereShininessSlider">
    <label for="sphereShininessSlider">Slide to Change Sphere Shininess</label>

    <input type="range" min="1.0" max="100.0" value="10.0" class="slider" id="planeShininessSlider" name="planeShininessSlider">
    <label for="planeShininessSlider">Slide to Change Plane Shininess</label>

    <input type="range" min="1" max="6" value="4.0" class="slider" id="numBouncesSlider" name="numBouncesSlider">
    <label for="numBouncesSlider">Slide to Number of Reflections</label>

    <input type="range" min="-200" max="200" value="0" class="slider" id="lightXSlider" name="lightXSlider">
    <label for="lightXSlider">Slide to Change the Light Source X Position</label>

    <input type="range" min="-200" max="200" value="40" class="slider" id="lightYSlider" name="lightYSlider">
    <label for="lightYSlider">Slide to Change the Light Source Y Position</label>

    <input type="range" min="-200" max="200" value="40" class="slider" id="lightZSlider" name="lightZSlider">
    <label for="lightZSlider">Slide to Change the Light Source Z Position</label>

    <input type="range" min="100.0" max="20000.0" value="3000.0" class="slider" id="lightIntensitySlider" name="lightIntensitySlider">
    <label for="lightIntensitySlider">Requires Softshadow: Slide to change the Light Intensity</label>

    <input type="range" min="2" max="400" value="20" class="slider" id="lightRadiusSlider" name="lightRadiusSlider">
    <label for="lightRadiusSlider">Requires Softshadow: Slide to change the Light Source Radius</label>




</div>


</body>
</html>
